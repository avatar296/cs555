plugins {
    id 'application'
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

spotless {
    java {
        googleJavaFormat('1.19.2')
        target 'src/**/*.java'
        removeUnusedImports()
        importOrder()
    }
}

application {
    mainClass = 'csx55.overlay.node.Registry'
}

task runRegistry(type: JavaExec) {
    group = "Execution"
    description = "Starts the Registry server"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'csx55.overlay.node.Registry'
    if (project.hasProperty('args')) {
        args project.property('args').split()
    }
}

task runMessagingNode(type: JavaExec) {
    group = "Execution"
    description = "Starts a Messaging Node client"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'csx55.overlay.node.MessagingNode'
    if (project.hasProperty('args')) {
        args project.property('args').split()
    }
}

task createTar(type: Tar) {
    archiveBaseName.set('Christopher_Cowart_HW1')
    archiveExtension.set('tar')
    
    from('.') {
        include 'build.gradle'
        include 'README.txt'
        include 'src/main/java/**/*.java'
        
        exclude '**/._*'       
        exclude '**/.DS_Store'
        
        exclude 'src/test/**'          
        exclude '**/test/**'           
        exclude '**/*Test.java'        
        exclude '**/*Tests.java'       
    }
}