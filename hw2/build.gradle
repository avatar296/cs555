plugins {
    id 'application'
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    // no external dependencies needed
}

test {
    enabled = false
}

spotless {
    java {
        googleJavaFormat('1.19.2')
        target 'src/**/*.java'
        removeUnusedImports()
        importOrder()
    }
}

application {
    // default main class (can override with run tasks)
    mainClass = 'csx55.threads.Registry'
}

tasks.register('runRegistry', JavaExec) {
    group = "Execution"
    description = "Runs the Registry"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'csx55.threads.Registry'
    if (project.hasProperty('args')) {
        args project.property('args').split()
    }
}

tasks.register('runNode', JavaExec) {
    group = "Execution"
    description = "Runs a ComputeNode"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'csx55.threads.ComputeNode'
    if (project.hasProperty('args')) {
        args project.property('args').split()
    }
}

task createTar(type: Tar) {
    archiveBaseName.set('Christopher_Cowart_HW2')
    archiveExtension.set('tar')

    from('.') {
        include 'build.gradle'
        include 'README.txt'
        include 'src/main/java/**/*.java'

        exclude '**/._*'
        exclude '**/.DS_Store'
        exclude 'src/test/**'
        exclude '**/test/**'
        exclude '**/*Test.java'
        exclude '**/*Tests.java'
    }
}
